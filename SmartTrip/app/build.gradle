apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.fruct.oss.smarttrip"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        ndk {
            moduleName "TestLib" // Name of C++ module (i.e. libSeePlusPlus)
            cFlags "-std=c++11 -fexceptions" // Add provisions to allow C++11 functionality
            stl "gnustl_shared" // Which STL library to use: gnustl or stlport
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'

    // Material design navigation drawer
    compile('com.mikepenz:materialdrawer:3.0.7@aar') {
        transitive = true
    }

    // Material design dialogs
    compile 'com.afollestad:material-dialogs:0.7.6.0'

    // Map library
    compile 'org.mapsforge:mapsforge-map-android:0.5.1'

    // Location library
    compile 'com.google.android.gms:play-services-location:7.5.0'

    // Event bus
    compile 'de.greenrobot:eventbus:2.4.0'

    // Asynchronous job library
    compile 'com.birbit:android-priority-jobqueue:1.3'
}

def coreWrapperDir = new File("${projectDir}/src/main/java/org/fruct/oss/smarttrip/jni")

task createCoreWrapperDir {
    coreWrapperDir.mkdirs()
}

task runSwig(type:Exec, dependsOn: ['createCoreWrapperDir']) {
    commandLine 'swig'
    args '-c++', '-java', '-package', 'org.fruct.oss.smarttrip.jni', '-outdir', coreWrapperDir.absolutePath, "${projectDir}/src/main/jni/TestLib.i"
}